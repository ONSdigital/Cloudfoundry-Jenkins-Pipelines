script {
	// Array to hold our environmental variables
	// We do this to avoid trigger a Jenkins sandbox violation
	custom_env = []

	// Loading in the order they appear
	// .properties is appended
	property_files = [ 'common' ]

	// Default terminal type
	terminal_type = 'xterm'

	repos = [
		deployment: [
			dir: 'Cloudfoundry-Deployment',
			url: env.CLOUDFOUNDRY_DEPLOYMENT_REPO
		],
		orchestration: [
			dir: 'Orchestration',
			url: env.CLOUDFOUNDRY_ORCHESTRATION_REPO
		],
		config: [
			dir: 'Config',
			url: env.CLOUDFOUNDRY_ORCHESTRATION_CONFIGURATION_REPO
		]
	]
}

properties([
	disableResume(),
	buildDiscarder(logRotator(artifactDaysToKeepStr: '', artifactNumToKeepStr: '', daysToKeepStr: '10', numToKeepStr: '10')),
	parameters([
		[
			$class: 'GitParameterDefinition',
			name: 'CLOUDFOUNDRY_DEPLOYMENT_BRANCH',
			description: 'Deployment Branch',
			branch: '',
			branchFilter: '.*',
			defaultValue: 'origin/master',
			quickFilterEnabled: false,
			selectedValue: 'DEFAULT',
			sortMode: 'ASCENDING_SMART',
			type: 'PT_BRANCH',
			useRepository: repos.deployment.url
		],
		[
			$class: 'GitParameterDefinition',
			name: 'CLOUDFOUNDRY_ORCHESTRATION_BRANCH',
			description: 'Orchestration Branch',
			branch: '',
			branchFilter: '.*',
			defaultValue: 'origin/master',
			quickFilterEnabled: false,
			selectedValue: 'DEFAULT',
			sortMode: 'ASCENDING_SMART',
			tagFilter: '*',
			type: 'PT_BRANCH',
			useRepository: repos.orchestration.url
		],
		[
			$class: 'GitParameterDefinition',
			name: 'CLOUDFOUNDRY_ORCHESTRATION_CONFIGURATION_BRANCH',
			description: 'Configuration Branch',
			branch: '',
			branchFilter: '.*',
			defaultValue: 'origin/master',
			quickFilterEnabled: false,
			selectedValue: 'DEFAULT',
			sortMode: 'ASCENDING_SMART',
			tagFilter: '*',
			type: 'PT_BRANCH',
			useRepository: repos.config.url
		],
		booleanParam(
			name: 'DEBUG',
			description: 'Run in debug mode',
			defaultValue: false
		)
	])
])

node('master'){
	stage('Ensure clean workspace'){
		// We clean the entire workspace, otherwise we end up with a dirty workspace, even if the checkout dirs under that workspace are clean
		// We clean before we start in case there are left overs from the previous build, if we clean after a build completes, we may not always
		// clean if the build fails
		deleteDir()
	}

	stage("Checking out $repos.deployment.dir"){
		checkout(
			$class: 'GitSCM',
			branches: [[ name: params.CLOUDFOUNDRY_DEPLOYMENT_BRANCH ]],
			extensions: [
				[
					$class: 'RelativeTargetDirectory',
					relativeTargetDir: repos.deployment.dir
				],
				[
					$class: 'SubmoduleOption',
					recursiveSubmodules: true,
					disableSubmodules: false,
					parentCredentials: false
				]
			],
			userRemoteConfigs: [[
				url: repos.deployment.url
			]]
		)
	}
	stage("Checking out $repos.orchestration.dir"){
		checkout(
			$class: 'GitSCM',
			branches: [[ name: params.CLOUDFOUNDRY_ORCHESTRATION_BRANCH ]],
			extensions: [
				[
					$class: 'RelativeTargetDirectory',
					relativeTargetDir: repos.orchestration.dir
				]
			],
			userRemoteConfigs: [[
				url: repos.orchestration.url
			]]
		)
	}
	stage("Checking out $repos.config.dir"){
		checkout(
			$class: 'GitSCM',
			branches: [[ name: params.CLOUDFOUNDRY_ORCHESTRATION_CONFIGURATION_BRANCH ]],
			extensions: [
				[
					$class: 'RelativeTargetDirectory',
					relativeTargetDir: repos.config.dir
				]
			],
			userRemoteConfigs: [[
				url: repos.config.url
			]]
		)
	}

	for(String property_file : property_files){
		if(fileExists("$repos.config.dir/${property_file}.properties")){
			stage("Load ${property_file}.properties file"){
				// Load properties and turn them into environmental variables
				// Requires approval from Jenkins > Manage jenkins > In-process Script Approval
				readProperties(file: "$repos.config.dir/${property_file}.properties").each{
					k, v -> custom_env.add("$k=$v")
				}
			}
		}
	}

	stage('Install Dependencies'){
		withEnv(["TERM=$terminal_type"] + custom_env){
			ansiColor(terminal_type){
				dir(repos.deployment.dir){
					sh "../$repos.orchestration.dir/jenkins/install_dependencies.sh"
				}
			}
		}
	}
	stage('Archive the Artifacts'){
		// It would have been nice if archiveArtifacts() respected the dir() enclosure, but it doesn't
		archiveArtifacts(
			// These are symlinks to the real files, but Jenkins is stupid, so copies the files rather than the link
			artifacts: "$repos.deployment.dir/work/bin/bosh,$repos.deployment.dir/work/bin/cf",
			fingerprint: true
		)
	}
}

